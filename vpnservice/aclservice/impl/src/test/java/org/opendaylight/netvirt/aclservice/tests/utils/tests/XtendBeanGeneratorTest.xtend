package org.opendaylight.netvirt.aclservice.tests.utils.tests

import java.math.BigInteger
import java.util.List
import org.eclipse.xtend.lib.annotations.Accessors
import org.eclipse.xtend.lib.annotations.FinalFieldsConstructor
import org.junit.Test
import org.opendaylight.netvirt.aclservice.tests.utils.XtendBeanGenerator

import static org.junit.Assert.assertEquals
import static org.junit.Assert.assertTrue

/**
 * Unit test for XtendBeanGenerator.
 *
 * @author Michael Vorburger
 */
class XtendBeanGeneratorTest {

    val g = new XtendBeanGenerator()

    @Test def void simplestNumberExpression() {
        assertThatEndsWith(g.getExpression(123), "123")
    }

    @Test def void simpleCharacter() {
        assertThatEndsWith(g.getExpression(new Character("c")), "'c'")
    }

    @Test def void nullCharacter() {
        var Character nullCharacter
        assertThatEndsWith(g.getExpression(nullCharacter), "null")
    }

    @Test def void defaultCharacter() {
        var char defaultCharacter
        assertThatEndsWith(g.getExpression(defaultCharacter), "")
    }

    @Test def void emptyString() {
        assertThatEndsWith(g.getExpression(""), "")
    }

    @Test def void aNull() {
        assertThatEndsWith(g.getExpression(null), "null")
    }

    @Test def void emptyList() {
        assertThatEndsWith(g.getExpression(#[]), "#[\n]")
    }

    @Test def void list() {
        assertThatEndsWith(g.getExpression(#["hi"]), "#[\n    \"hi\"\n]")
    }

    @Test def void complexEmptyBean() {
        assertEquals('''
            // Code auto. generated by Michael Vorburger's org.opendaylight.netvirt.aclservice.tests.utils.XtendBeanGenerator
            new Bean
            '''.toString, g.getExpression(new Bean))
    }

    @Test def void complexBean() {
        val bean = new Bean => [
            ALongObject = 123L
            AShort = 123 as short
            anInt = 123
            anInteger = 123
            bigInteger = 456bi
            innerBean = new Bean => [
                name = "1beanz"
            ]
            name = "hello, world"
            beanz = #[
                new Bean => [
                    name = "beanz1"
                ]
            ]
        ]

        assertEquals('''
            // Code auto. generated by Michael Vorburger's org.opendaylight.netvirt.aclservice.tests.utils.XtendBeanGenerator
            new Bean => [
                ALongObject = 123L
                AShort = 123 as short
                anInt = 123
                anInteger = 123
                beanz = #[
                    new Bean => [
                        name = "beanz1"
                    ]
                ]
                bigInteger = 456bi
                innerBean = new Bean => [
                    name = "1beanz"
                ]
                name = "hello, world"
            ]'''.toString, g.getExpression(bean))
    }

    @Test def void beanWithArrayProperty() {
        val bean = new BeanWithArrayProperty => [
            names = #[ "Michael" ]
        ]
        assertEquals('''
            // Code auto. generated by Michael Vorburger's org.opendaylight.netvirt.aclservice.tests.utils.XtendBeanGenerator
            new BeanWithArrayProperty => [
                names = #[
                    "Michael"
                ]
            ]'''.toString, g.getExpression(bean))
    }

    @Test def void beanWithConstructor() {
        val bean = new BeanWithOneConstructor("hello, world", 123) => [
            address = "Street 1"
        ]
        assertEquals('''
            // Code auto. generated by Michael Vorburger's org.opendaylight.netvirt.aclservice.tests.utils.XtendBeanGenerator
            new BeanWithOneConstructor("hello, world", 123) => [
                address = "Street 1"
            ]'''.toString, g.getExpression(bean))
    }

    @Test def void beanWithMultiConstructor() {
        val bean1 = new BeanWithMultiConstructor("foobar")
        assertEquals('''
            // Code auto. generated by Michael Vorburger's org.opendaylight.netvirt.aclservice.tests.utils.XtendBeanGenerator
            new BeanWithMultiConstructor("foobar")'''.toString, g.getExpression(bean1))

        val bean2 = new BeanWithMultiConstructor(123)
        assertEquals('''
            // Code auto. generated by Michael Vorburger's org.opendaylight.netvirt.aclservice.tests.utils.XtendBeanGenerator
            new BeanWithMultiConstructor(123)'''.toString, g.getExpression(bean2))
    }

    def private void assertThatEndsWith(String string, String endsWith) {
        assertTrue("'''" + string + "''' expected to endWith '''" + endsWith + "'''", string.endsWith(endsWith));
    }


    @Accessors
    public static class Bean {
        String name
        int anInt
        Integer anInteger

        @Accessors(PUBLIC_GETTER) /* but no setter */ String onlyGetterString = "onlyGetterNoSetterString"

        boolean aBoolean
        Boolean aBooleanObject

        char aChar
        Character aCharacter

        short aShort
        BigInteger bigInteger
        long aLong
        Long aLongObject
        Long nullLong
        Byte aByteObject
        byte aByte
        double aDouble
        Double aDoubleObject
        float aFloat
        Float aFloatObject

        Bean innerBean
        @Accessors(PUBLIC_GETTER) List<Bean> beanz = newArrayList
    }

    @Accessors
    public static class BeanWithArrayProperty {
        String[] names;
    }

    public static class BeanWithOneConstructor {
        @Accessors(PUBLIC_GETTER) final String name
        @Accessors(PUBLIC_GETTER) final Integer age
        @Accessors String address

        @FinalFieldsConstructor
        new() { }
    }

    public static class BeanWithMultiConstructor {
        // Example bean of some structure which has either a name or an id
        // Does not necessarily make sense / is not ideal in real world, as e.g. a
        // private internal subclass with and either or, and factory methods, would be a better design,
        // but ODL has some like this, so needed and tested.

        @Accessors(PUBLIC_GETTER) final String name
        @Accessors(PUBLIC_GETTER) final Integer id

        new(String name) {
            this.name = name
            this.id = null
        }

        new(Integer id) {
            this.name = null
            this.id = id
        }

        // default constructor (does not necessarily make sense in real world, but useful for test)
        new() {
            name = null
            id = null
        }

        // clone/copy constructor (just to have another one)
        new(BeanWithMultiConstructor original) {
            this.name = original.name
            this.id = original.id
        }
    }

}
