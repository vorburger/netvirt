package org.opendaylight.netvirt.aclservice.tests.utils

import java.math.BigInteger
import java.util.List
import java.util.Map
import org.mockito.cglib.core.ReflectUtils

/**
 * Magic. pure. Magic.
 *
 * Generates highly readable Java Bean object initialization code
 * based on the <a href="https://eclipse.org/xtend/documentation/203_xtend_expressions.html#with-operator">
 * Xtend With Operator</a>.  This syntax is very well suited e.g. to capture expected objects in test code.
 *
 * <p>Xtend is a cool JVM language which itself
 * transpiles to Java source code.  There are <a href="https://eclipse.org/xtend/download.html">plugins
 * for Eclipse and IntelliJ IDEA to work with Xtend</a> available.  It is also possible
 * to use Gradle's Continuous Build mode on the Command Line to get Xtend translated to Java on the fly.
 *
 * @author Michael Vorburger
 */
class XtendBeanGenerator {

    // TODO Detect Constructor..
    // TODO Support Builder - automatically, just check CP for *Builder

    def void print(Object bean) {
        System.out.println(getExpression(bean))
    }

    def String getExpression(Object bean) {
        '''
        // Code auto. generated by Michael Vorburger's «class.name»
        «getExpressionInternal(bean)»'''
    }

    def protected CharSequence getExpressionInternal(Object bean) {
        '''
        new «bean.class.simpleName»() => [
            «FOR field : getBeanFields(bean).entrySet»
            «IF (field.value != null)»
            «field.key» = «stringify(field.value)»
            «ENDIF»
            «ENDFOR»
        ]'''
    }

    def protected stringify(Object object) {
        switch object {
//            Object[]  : '''#[ «FOR e : object»
//                «getXtendExpression(e)»
//            «ENDFOR»
//                           ]'''
            List<?>   : '''
                        #[
                            «FOR e : object»
                            «getExpressionInternal(e)»
                            «ENDFOR»
                        ]'''
            String    : '''"«object»"'''
            Integer   : '''«object»'''
            Long      : '''«object»L'''
            Short     : '''«object» as short'''
            BigInteger: '''«object»bi'''
            default   : '''«getExpressionInternal(object)»'''
        }
    }

    def protected Map<String, Object> getBeanFields(Object bean) {
        // could also implement using:
        //   * org.eclipse.xtext.xbase.lib.util.ReflectExtensions.get(Object, String)
        //   * com.google.common.truth.ReflectionUtil.getField(Class<?>, String)
        //   * org.codehaus.plexus.util.ReflectionUtils
        val properties = ReflectUtils.getBeanSetters(bean.class)
        val map = newLinkedHashMap()
        for (property : properties) {
            map.put(property.name, property.readMethod.invoke(bean))
        }
        return map
    }

}
